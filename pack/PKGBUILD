# Maintainer:  Tim Schumacher <timschumi@gmx.de>
# Contributor: KillWolfVlad <github.com/KillWolfVlad>
# Contributor: WaveHack <email@wavehack.net>
# Contributor: Whovian9369 <Whovian9369@gmail.com>
# Contributor: Angelo Theodorou <encelo@gmail.com>

pkgname=gittyup
pkgrel=3
pkgver=1.1.1
branch="arch-aur-build"
pkgdesc='Understand your Git history!'
url='https://murmele.github.io/Gittyup'
arch=('x86_64')
license=('MIT')
depends=('desktop-file-utils' 'qt5-base' 'git' 'openssl' 'libssh2' 'hunspell' 'cmark' 'lua' 'lua-lpeg')
makedepends=('cmake' 'ninja' 'git' 'qt5-tools' 'qt5-translations')
source=(
  "git+https://github.com/exactly-one-kas/Gittyup#branch=${branch}"
)
sha256sums=(
  'SKIP'
)

prepare() {
  cd "$srcdir/Gittyup"

  git config submodule.dep/openssl/openssl.update none
  git config submodule.dep/libssh2/libssh2.update none
  git config submodule.dep/git/git.update none
  git config submodule.dep/hunspell/hunspell.update none
  git config submodule.dep/cmark/cmark.update none

  git submodule update --init --recursive

  if [ ! -d "$srcdir/gittyup-build" ]; then
    mkdir "$srcdir/gittyup-build"
  fi

  cd "$srcdir/gittyup-build"
  cmake -G Ninja \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_PREFIX_PATH=/usr \
    -DDEBUG_OUTPUT=OFF \
    -DUSE_SYSTEM_OPENSSL=ON \
    -DUSE_SYSTEM_LIBSSH2=ON \
    -DUSE_SYSTEM_GIT=ON \
    -DUSE_SYSTEM_LUA=ON \
    -DUSE_SYSTEM_HUNSPELL=ON \
    -DUSE_SYSTEM_CMARK=ON \
    -DLUA_MODULES_PATH=/usr/lib/lua/5.4 \
    ../Gittyup
}

build() {
  cd "$srcdir/gittyup-build"
  ninja
}

check() {
  cd "$srcdir/gittyup-build"
  ninja check
}

package() {
  cd "$srcdir/gittyup-build"

  ninja package

  mkdir -p "${pkgdir}/usr/"{share/gittyup,bin}
  rm -rf "${srcdir}/pack"
  mkdir -p "${srcdir}/pack"

  tar xf "${srcdir}/gittyup-build/pack/Gittyup-${pkgver}.tar.gz" -C "${srcdir}/pack"
  cd "${srcdir}/pack"/Gittyup-*

  ln -s "/usr/share/gittyup/Gittyup" "${pkgdir}/usr/bin/gittyup"

  rm -rf *.so.*
  rm -rf Plugins
  cp -Rv . "${pkgdir}/usr/share/gittyup"

  install -D -m644 "${pkgdir}/usr/share/gittyup/Resources/Gittyup.iconset/gittyup_logo.svg" "${pkgdir}/usr/share/icons/hicolor/scalable/apps/gittyup.svg"
  install -D -m644 "${pkgdir}/usr/share/gittyup/Resources/Gittyup.iconset/icon_16x16.png" "${pkgdir}/usr/share/icons/hicolor/16x16/apps/gittyup.png"
  install -D -m644 "${pkgdir}/usr/share/gittyup/Resources/Gittyup.iconset/icon_32x32.png" "${pkgdir}/usr/share/icons/hicolor/32x32/apps/gittyup.png"
  install -D -m644 "${pkgdir}/usr/share/gittyup/Resources/Gittyup.iconset/icon_64x64.png" "${pkgdir}/usr/share/icons/hicolor/64x64/apps/gittyup.png"
  install -D -m644 "${pkgdir}/usr/share/gittyup/Resources/Gittyup.iconset/icon_128x128.png" "${pkgdir}/usr/share/icons/hicolor/128x128/apps/gittyup.png"
  install -D -m644 "${pkgdir}/usr/share/gittyup/Resources/Gittyup.iconset/icon_256x256.png" "${pkgdir}/usr/share/icons/hicolor/256x256/apps/gittyup.png"
  install -D -m644 "${pkgdir}/usr/share/gittyup/Resources/Gittyup.iconset/icon_512x512.png" "${pkgdir}/usr/share/icons/hicolor/512x512/apps/gittyup.png"

  install -D -m644 ${srcdir}/Gittyup/LICENSE.md "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -D -m644 "${srcdir}/Gittyup/rsrc/linux/com.github.Murmele.Gittyup.desktop" "${pkgdir}/usr/share/applications/gittyup.desktop"

  sed -i 's/Exec=Gittyup/Exec=gittyup/' "${pkgdir}/usr/share/applications/gittyup.desktop"
}
